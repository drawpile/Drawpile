#!/usr/bin/env perl
# Copyright (c) 2022 askmeaboutloom
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
use 5.020;
use warnings;
use Cwd qw(abs_path);
use File::Basename qw(dirname);
use File::Find;
use FindBin;

sub want_file {
    my ($path) = @_;
    return -f $path
        && $path !~ m{[/\\](?:build\w*|\w+_git|imgui|emsdl2|node_modules|libmypaint|site-packages)[/\\]}
        && $path =~ /(?:\.(?:c|cpp|h|pl|lua|cmake|go|css|tsx?)|CMakeLists\.txt)\z/;
}

sub is_missing_copyright {
    my ($path) = @_;
    open my $fh, '<', $path or die "Can't open '$path': $!";
    my $found = 0;
    while (<$fh>) {
        return 0 if m{\A.*copyright}i;
        return 0 if m{auto-generated}i;
        return 1 if $. == 9;
    }
    return 1;
}

my $missing_count = 0;

sub wanted {
    my $path = $_;
    if (want_file($path) && is_missing_copyright($path)) {
        print 'Missing copyright header in ', $path, "\n";
        ++$missing_count;
    }
}

find({
    wanted => \&wanted,
    no_chdir => 1,
}, dirname(abs_path($FindBin::Bin)));

print "Found missing copyright header(s) in $missing_count file(s)\n";
