function(target_auto_source_group)
	foreach(target IN LISTS ARGN)
		get_target_property(sources ${target} SOURCES)
		get_target_property(resources ${target} RESOURCE)
		foreach(filename IN LISTS sources)
			get_source_file_property(is_macos_resource "${filename}" MACOSX_PACKAGE_LOCATION)
			list(FIND resources "${filename}" is_resource)
			if(is_resource GREATER -1 OR is_macos_resource)
				set(prefix "Resources")
			else()
				set(prefix "Source Files")
			endif()

			string(FIND "${filename}" "${CMAKE_CURRENT_BINARY_DIR}" bin_dir_pos)
			if(bin_dir_pos EQUAL 0)
				source_group("${prefix}" FILES ${filename})
			else()
				source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" PREFIX "${prefix}" FILES ${filename})
			endif()
		endforeach()
	endforeach()
endfunction()
